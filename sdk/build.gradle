apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

version = "1.7.1-SNAPSHOT"

android {
    compileSdkVersion 32
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 32
        versionName version
        versionCode Integer.valueOf(version.toString().replace('.', '').replace('-SNAPSHOT', ''))

        buildConfigField "String", "VERSION_NAME", "\"$versionName\""
        
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    testOptions {
        unitTests.includeAndroidResources = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

// https://developer.android.com/studio/build/maven-publish-plugin
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId 'com.adnuntius.android'
                artifactId 'sdk'

                from components.release

                pom {
                    name = 'Adnuntius Android SDK'
                    packaging = 'aar'
                    description = 'Adnuntius Android SDK is an android sdk which allows business partners to embed Adnuntius ads in their native android applications.'
                    url = 'http://www.example.com/example-application'

                    scm {
                        connection = 'scm:svn:https://github.com/Adnuntius/android_sdk.git'
                        developerConnection = 'scm:svn:https://github.com/Adnuntius/android_sdk.git'
                        url = 'https://github.com/Adnuntius/android_sdk'
                    }

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    organization {
                        name = 'Adnuntius AS'
                        url = 'https://adnuntius.com'
                    }

                    developers {
                        developer {
                            id = 'adnuntius'
                            name = 'Adnuntius'
                            email = 'ops@adnuntius.com'
                        }
                    }
                }

                artifact(sourceJar) {
                    classifier = 'sources'
                }
                artifact(javadocJar) {
                    classifier = 'javadoc'
                }
            }
        }

        repositories {
            maven {
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"

                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }
    }
}

dependencies {
    implementation 'com.android.volley:volley:1.2.1'
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'androidx.annotation:annotation:1.4.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation "org.robolectric:robolectric:4.5.1"
    testImplementation 'org.apache.httpcomponents:httpclient:4.5.13'
    testImplementation "org.mockito:mockito-core:3.11.0"
    testImplementation "net.javacrumbs.json-unit:json-unit:2.25.0"

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.2.0'
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set("sources")
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.srcDirs
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    archiveClassifier.set("javadoc")
}

artifacts {
    archives sourceJar
    archives javadocJar
}

afterEvaluate {
    signing {
        sign publishing.publications.release
    }
}
